buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE")
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.12'
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
    id 'net.researchgate.release' version '2.4.1'
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: "org.springframework.boot"
apply from: 'build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-docker.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-info.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-service.gradle'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Dalston.SR2"
        mavenBom "org.springframework.boot:spring-boot-dependencies:1.5.6.RELEASE"
    }
}

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    flatDir {
        dirs 'libs'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'EPAM Report Portal. TFS Integration'

dependencies {
    compile 'com.epam.reportportal:commons-bugtracking:3.1.0'
    compile 'com.epam.reportportal:commons:3.1.0'
    compile 'org.jasypt:jasypt:1.9.2'
    compile name: 'com.microsoft.tfs.sdk-14.120.0'
}

task copyTfs(type: Copy) {
    into "$buildDir/libs"
    from "$projectDir/docker/native.tar.gz"
}

task copyRuntimeLibs(type: Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

build.dependsOn(copyRuntimeLibs).dependsOn(copyTfs)

jar {
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    manifest {
        attributes 'Implementation-Title': 'ReportPortal TFS Integration',
                'Implementation-Version': version,
                'Main-Class': 'com.epam.reportportal.extension.bugtracking.tfs.TfsServiceApp',
                'Class-Path': configurations.runtime.files.findAll { it.name.contains("sdk-14") == false }.collect {
                    "lib/$it.name"
                }.join(' ').concat(' libs/com.microsoft.tfs.sdk-14.120.0.jar')
    }
}






